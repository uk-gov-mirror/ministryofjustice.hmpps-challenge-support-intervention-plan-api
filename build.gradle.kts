import org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_25
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  id("uk.gov.justice.hmpps.gradle-spring-boot") version "11.0.9"
  kotlin("plugin.spring") version "2.4.20"
  kotlin("plugin.jpa") version "2.4.20"
  jacoco
}

ext["jackson-bom.version"] = "3.2.1"
ext["jackson-2-bom.version"] = "2.22.1"
ext["logback.version"] = "1.5.36"
ext["tomcat.version"] = "11.0.23"
ext["postgresql.version"] = "42.7.12"
ext["httpcore5.version"] = "5.4.3"

configurations {
  testImplementation { exclude(group = "org.junit.vintage") }
}

dependencies {

  // Spring boot dependencies
  implementation("org.springframework.boot:spring-boot-starter-data-jpa")
  implementation("org.springframework.boot:spring-boot-starter-webclient")
  implementation("uk.gov.justice.service.hmpps:hmpps-kotlin-spring-boot-starter:3.0.1")
  implementation("io.sentry:sentry-spring-boot-4:8.57.0")
  implementation("com.fasterxml.uuid:java-uuid-generator:5.2.0")
  implementation("org.springframework.data:spring-data-envers")

  // OpenAPI
  implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:3.1.1")

  // AWS
  implementation("uk.gov.justice.service.hmpps:hmpps-sqs-spring-boot-starter:7.4.1")

  // Database dependencies
  runtimeOnly("org.springframework.boot:spring-boot-starter-flyway")
  runtimeOnly("org.flywaydb:flyway-database-postgresql")
  runtimeOnly("org.postgresql:postgresql")

  // Test dependencies
  testImplementation("org.testcontainers:postgresql:1.21.4")
  testImplementation("org.testcontainers:localstack:1.21.4")
  testImplementation("uk.gov.justice.service.hmpps:hmpps-kotlin-spring-boot-starter-test:3.0.1")
  testImplementation("org.springframework.boot:spring-boot-starter-webflux-test")
  testImplementation("org.wiremock:wiremock-standalone:3.13.2")
  testImplementation("org.awaitility:awaitility-kotlin:4.3.0")

  constraints {
    implementation("io.opentelemetry:opentelemetry-api:1.66.0") {
      because("CVE-2026-45292 - remove when transitive dependency is updated")
    }
  }
}

kotlin {
  jvmToolchain(25)
}

tasks {
  withType<KotlinCompile> {
    compilerOptions {
      jvmTarget = JVM_25
    }
  }

  // Schema documentation helpers - see .github/workflows/schema-spy.yml. These build the database and
  // export the reference data for the data dictionary, and are not part of the normal suite.
  test {
    if (project.hasProperty("init-db")) {
      include("**/InitialiseDatabase.class", "**/ExportReferenceData.class")
      systemProperty("referenceDataOutput", project.findProperty("referenceDataOutput") ?: "reference-data.csv")
    } else {
      exclude("**/InitialiseDatabase.class", "**/ExportReferenceData.class")
    }
  }
}

tasks.named("test") {
  finalizedBy("jacocoTestReport")
}

tasks.named<JacocoReport>("jacocoTestReport") {
  reports {
    html.required.set(true)
    xml.required.set(true)
  }
}
